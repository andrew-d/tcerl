dnl -- include additional autoconf commands here
dnl -- do not include AC_OUTPUT, this is called for you

ERL_CRASH_DUMP=/dev/null
EIBASE="`erl -noshell -noinput -eval 'io:format (\"~s\", [[ code:lib_dir (erl_interface) ]])' -s erlang halt`"
AC_SUBST([EIBASE])

ERTSBASE="`erl -noshell -noinput -eval 'io:format (\"~s\", [[ \"/\" ++ filename:join (lists:reverse ([ \"erts-\" ++ erlang:system_info (version) | tl (lists:reverse (string:tokens (code:lib_dir (), \"/\"))) ])) ]]).' -s erlang halt `"
AC_SUBST([ERTSBASE])

CPPFLAGS="$CPPFLAGS -I ${EIBASE}/include -I ${EIBASE}/src/legacy -I ${EIBASE}/src/misc/ -I ${EIBASE}/src/decode -I ${ERTSBASE}/include"

AC_CHECK_HEADER([erl_marshal.h], 
                [], 
                [AC_MSG_ERROR([can't find erl_marshal.h; under debian you need to install erlang-src])], 
                [#include <erl_interface.h>])

LDFLAGS="$LDFLAGS -L${EIBASE}/lib -L${ERTSBASE}/lib"
AC_CHECK_LIB([ei],
             [ei_set_compat_rel],
             [],
             [ AC_MSG_ERROR([can't find libei]) ],
             [ -lpthread ])
AC_CHECK_LIB([erl_interface],
             [erl_compare_ext],
             [],
             [ AC_MSG_ERROR([can't find liberl_interface]) ],
             [ -lei -lpthread ])

FW_TEMPLATE_C_SETUP_PKGCONFIG

test "$FW_NATIVE_PACKAGE_TYPE" != "none" || {
  PKG_CHECK_MODULES([TOKYOCABINET],
                    [tokyocabinet],
                    ,
                    [AC_MSG_WARN([pkg-config of 'tokyocabinet' failed: $TOKYOCABINET_PKG_ERRORS])
                     PKG_CHECK_MODULES([TOKYOCABINET],
                                       [tokyocabinet-1.0],
                                       ,
                                        AC_MSG_ERROR([pkg-config of 'tokyocabinet-1.0' failed: $TOKYOCABINET_PKG_ERRORS]))])
}
AC_SUBST([TOKYOCABINET_CFLAGS])
AC_SUBST([TOKYOCABINET_LIBS])

AC_CHECK_HEADERS([erl_driver.h], ,
                 [AC_MSG_ERROR([ need erl_driver.h to proceed ])])
